#compdef wikicurses

_wikicurses_wiki() {
	IFS=$'\n'
	wikis=($(wikicurses --complete wiki $words[CURRENT] 2>/dev/null))
	_wanted wikis expl "wikis" compadd "${(@)wikis}"
}

_wikicurses_get_wikiarg() {
	for (( i = 0 ; i < ${#words[@]} ; i++ )) do
		if [[ "$words[$i]" = "--wiki" || "$words[$i]" = "-w" ]] then
			wiki="$words[$(($i+1))]"
			break
		fi
	done
}

_wikicurses_feed() {
	_wikicurses_get_wikiarg
	IFS=$'\n'
	if [[ $wiki ]] then
		feeds=($(wikicurses -w $wiki --complete feed $words[CURRENT] 2>/dev/null))
	else
		feeds=($(wikicurses --complete feed $words[CURRENT] 2>/dev/null))
	fi
	_wanted feeds expl "feeds" compadd "${(@)feeds}"
}

_wikicurses_name() {
	_wikicurses_get_wikiarg
	IFS=$'\n'
	if [[ $wiki ]] then
		pages=($(wikicurses -w $wiki --complete search $words[CURRENT] 2>/dev/null))
	else
		pages=($(wikicurses --complete search $words[CURRENT] 2>/dev/null))
	fi
	_wanted pages expl "pages" compadd "${(@)pages}"
}

_wikicurses() {
	_arguments -s : \
		{-h,--help}'[Display help message]'\
		{-w,--wiki}'[Url or name of wiki to use]:wikis:_wikicurses_wiki'\
		{-f,--feed}'[Show featured articles (Wikipedia only)]:feeds:_wikicurses_feed'\
		'*:pages:_wikicurses_name'
}
