#example mission marker.flo
# shows use of if update and if change syntax

house marker

framer vehiclesim be active first vehicle_run

frame vehicle_run
    do simulator motion uuv

framer mission be active first change1

frame start
    set elapsed to 2.0
    set goal.heading to 0.0
    set goal.depth to 5.0
    set goal.speed to 2.5

    go abort if elapsed >= goal

frame change1 in start
    put 6 into .marked.changed
    go next if change .marked.changed

frame update1 in start
    put true into .marked.updated
    go next if update .marked.updated

frame update2 in start
    go next if update .state.heading

frame change2 in start
    go next if change .state.depth

frame update3 in start
    go next if update in update3 .state.heading

frame update4 in start
    go next if update in start .state.heading

frame update5 in start
    go next if update .marked.updated
    go next if recurred >= 3

frame done
    print "Success"
    bid stop vehiclesim
    bid stop autopilot
    bid stop me

frame abort
    print "Failure"
    put "Marker" into meta.failure
    bid stop vehiclesim
    bid stop autopilot
    bid stop me

framer autopilot be active first autopilot_run

frame autopilot_run
    do controller pid speed
    do controller pid heading
    do controller pid depth
    do controller pid pitch
