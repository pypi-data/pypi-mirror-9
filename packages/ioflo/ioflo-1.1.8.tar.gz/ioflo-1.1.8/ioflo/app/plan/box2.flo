#example mission box2.flo
# with max depth protection

house box2

init max.depth to 50.0

framer vehiclesim be active first vehicle_run

frame vehicle_run
   do simulator motion uuv

framer mission be active first northleg

frame depthmax
   go abort if state.depth >= .max.depth

frame northleg in depthmax
   set elapsed to 20.0
   set goal.heading to 0.0
   set goal.depth to 5.0
   set goal.speed to 2.5
   go next if elapsed >= goal

frame eastleg in depthmax
   set goal.heading to 90.0
   go next if elapsed >= goal

frame southleg in depthmax
   set goal.heading to 180.0
   go next if elapsed >= goal

frame westleg in depthmax
   set goal.heading to 270.0
   go next if elapsed >= goal

frame mission_stop
   bid stop vehiclesim
   bid stop autopilot
   bid stop logger
   bid stop me

frame abort
   set goal.depth to 0.0
   set goal.speed to 2.5
   go mission_stop if state.depth == 0.0 +- 0.25

framer autopilot be active first autopilot_run

frame autopilot_run
   do controller pid speed
   do controller pid heading
   do controller pid depth
   do controller pid pitch


logger logger to /var/tmp/log/ioflo/
log state  on update
loggee position state.position heading state.heading depth state.depth speed state.speed

log goal on update
loggee heading goal.heading depth goal.depth speed goal.speed

log frame on update
loggee active framer.mission.state.active human framer.mission.state.human
