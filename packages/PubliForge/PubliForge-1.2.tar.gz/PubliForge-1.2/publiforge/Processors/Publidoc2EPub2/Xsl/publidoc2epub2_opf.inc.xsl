<?xml version="1.0" encoding="UTF-8"?>
<!-- $Id: publidoc2epub2_opf.inc.xsl b662575e42a4 2015/02/02 12:25:15 Patrick $ -->
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns="http://www.idpf.org/2007/opf"
                xmlns:date="http://exslt.org/dates-and-times"
                extension-element-prefixes="date">

  <!--
      =========================================================================
      Template content_opf
      =========================================================================
  -->
  <xsl:template name="content_opf">
    <xsl:document href="{$path}content.opf" method="xml"
                  encoding="utf-8" indent="yes">
      <xsl:comment> Generated by PubliForge, <xsl:value-of select="date:date-time()"/> </xsl:comment>
      <package version="2.0" unique-identifier="uid">
        <xsl:call-template name="metadata"/>
        <xsl:call-template name="manifest"/>
        <xsl:call-template name="spine"/>
        <xsl:call-template name="guide"/>
      </package>
    </xsl:document>
  </xsl:template>


  <!--
      *************************************************************************
                                      METADATA
      *************************************************************************
  -->
  <!--
      =========================================================================
      Template metadata, metadata_extra
      =========================================================================
  -->
  <xsl:template name="metadata">
    <metadata xmlns:dc="http://purl.org/dc/elements/1.1/"
              xmlns:dcterms="http://purl.org/dc/terms/"
              xmlns:opf="http://www.idpf.org/2007/opf">
      <!-- ean/identifier -->
      <xsl:call-template name="metadata_identifier"/>
      <!-- title -->
      <dc:title>
        <xsl:apply-templates select="head/title" mode="text"/>
      </dc:title>
      <!-- language -->
      <dc:language><xsl:value-of select="$lang"/></dc:language>
      <!-- creators/contributors -->
      <xsl:apply-templates select="head/contributors" mode="metadata"/>
      <!-- publisher -->
      <xsl:choose>
        <xsl:when test="head/contributors/contributor[role='publisher']/label">
          <dc:publisher>
            <xsl:value-of select="head/contributors/contributor[role='publisher']/label"/>
          </dc:publisher>
        </xsl:when>
        <xsl:when test="$publisher_label">
          <dc:publisher><xsl:value-of select="$publisher_label"/></dc:publisher>
        </xsl:when>
      </xsl:choose>
      <!-- date -->
      <xsl:if test="head/date">
        <dc:date><xsl:value-of select="head/date/@value"/></dc:date>
      </xsl:if>
      <!-- copyright -->
      <xsl:choose>
        <xsl:when test="head/copyright">
          <xsl:apply-templates select="head/copyright" mode="metadata"/>
        </xsl:when>
        <xsl:when test="head/contributors/contributor[role='publisher']/label">
          <dc:rights>Copyright <xsl:value-of select="head/contributors/contributor[role='publisher']/label"/></dc:rights>
        </xsl:when>
        <xsl:when test="$publisher_label">
          <dc:rights>Copyright <xsl:value-of select="$publisher_label"/></dc:rights>
        </xsl:when>
      </xsl:choose>
      <!-- subject, keyword -->
      <xsl:apply-templates select="head/subjectset/subject" mode="metadata"/>
      <xsl:apply-templates select="head/keywordset/keyword" mode="metadata"/>
      <!-- abstract -->
      <xsl:if test="head/abstract">
        <dc:description>
          <xsl:apply-templates select="head/abstract/p" mode="text"/>
        </dc:description>
      </xsl:if>
      <!-- cover -->
      <xsl:choose>
        <xsl:when test="$cover and not($cover='=')">
          <meta name="cover" content="i_{translate($fid, ' ', '_')}_cover"/>
        </xsl:when>
        <xsl:when test="head/cover and ($img or $cover='=')">
          <meta name="cover" content="i_{translate(head/cover/image/@id, ' ', '_')}"/>
        </xsl:when>
      </xsl:choose>
      <!-- extra metadata -->
      <xsl:call-template name="metadata_extra"/>
    </metadata>
  </xsl:template>

  <xsl:template name="metadata_extra"/>

  <!--
      =========================================================================
      Template metadata_identifier
      =========================================================================
  -->
  <xsl:template name="metadata_identifier"
                xmlns:dc="http://purl.org/dc/elements/1.1/"
                xmlns:opf="http://www.idpf.org/2007/opf">
    <xsl:choose>
      <xsl:when test="$ean or head/identifier[
                      @type='ean' and (@for='epub' or @for='epub2' or not(@for))]">
        <dc:identifier opf:scheme="EAN" id="uid">
          <xsl:call-template name="identifier"/>
        </dc:identifier>
      </xsl:when>
      <xsl:otherwise>
        <dc:identifier opf:scheme="URI" id="uid">
          <xsl:call-template name="identifier"/>
        </dc:identifier>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <!--
      =========================================================================
      contributor(s) mode metadata
      =========================================================================
  -->
  <xsl:template match="contributors" mode="metadata">
    <xsl:apply-templates select="contributor" mode="metadata"/>
  </xsl:template>

  <xsl:template match="contributor" mode="metadata"
                xmlns:dc="http://purl.org/dc/elements/1.1/"
                xmlns:opf="http://www.idpf.org/2007/opf">
    <xsl:for-each select="role">
      <xsl:choose>
        <xsl:when test=".='publisher'"/>

        <xsl:when test=".='packager'">
          <dc:contributor opf:role="mrk">
            <xsl:attribute name="opf:file-as">
              <xsl:apply-templates select="../lastname|../label" mode="text"/>
              <xsl:if test="../firstname">
                <xsl:text>, </xsl:text>
                <xsl:apply-templates select="../firstname" mode="text"/>
              </xsl:if>
            </xsl:attribute>
            <xsl:if test="../firstname">
              <xsl:apply-templates select="../firstname" mode="text"/>
              <xsl:text> </xsl:text>
            </xsl:if>
            <xsl:apply-templates select="../lastname|../label" mode="text"/>
          </dc:contributor>
        </xsl:when>

        <xsl:otherwise>
          <dc:creator>
            <xsl:attribute name="opf:role">
              <xsl:choose>
                <xsl:when test=".='illustrator'">ill</xsl:when>
                <xsl:otherwise>aut</xsl:otherwise>
              </xsl:choose>
            </xsl:attribute>
            <xsl:attribute name="opf:file-as">
              <xsl:apply-templates select="../lastname|../label" mode="text"/>
              <xsl:if test="../firstname">
                <xsl:text>, </xsl:text>
                <xsl:apply-templates select="../firstname" mode="text"/>
              </xsl:if>
            </xsl:attribute>
            <xsl:if test="../firstname">
              <xsl:apply-templates select="../firstname" mode="text"/>
              <xsl:text> </xsl:text>
            </xsl:if>
            <xsl:apply-templates select="../lastname|../label" mode="text"/>
          </dc:creator>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:for-each>
  </xsl:template>

  <!--
      =========================================================================
      copyright mode metadata
      =========================================================================
  -->
  <xsl:template match="copyright" mode="metadata"
                xmlns:dc="http://purl.org/dc/elements/1.1/">
    <dc:rights>Copyright <xsl:value-of select="normalize-space()"/></dc:rights>
  </xsl:template>

  <!--
      =========================================================================
      subject, keyword mode metadata
      =========================================================================
  -->
  <xsl:template match="subject" mode="metadata"
                xmlns:dc="http://purl.org/dc/elements/1.1/">
    <dc:subject><xsl:value-of select="."/></dc:subject>
  </xsl:template>

  <xsl:template match="keyword" mode="metadata"/>


  <!--
      *************************************************************************
                                       MANIFEST
      *************************************************************************
  -->
  <!--
      =========================================================================
      Template manifest, manifest_extra
      =========================================================================
  -->
  <xsl:template name="manifest">
    <manifest>
      <item id="ncx" href="toc.ncx" media-type="application/x-dtbncx+xml"/>
      <!-- XHTML -->
      <xsl:if test="head/cover or ($cover and not($cover='='))">
        <item id="h_{translate($fid, ' ', '_')}-cover"
              href="{$fid}-cover{$html_ext}" media-type="application/xhtml+xml"/>
      </xsl:if>
      <xsl:if test="$toc">
        <item id="h_{translate($fid, ' ', '_')}-toc"
              href="{$fid}-toc{$html_ext}" media-type="application/xhtml+xml"/>
      </xsl:if>
      <xsl:apply-templates select="//division|//topic" mode="manifest"/>
      <xsl:if test="$index and .//index">
        <item id="h_{translate($fid, ' ', '_')}-index"
              href="{$fid}-index{$html_ext}" media-type="application/xhtml+xml"/>
      </xsl:if>
      <xsl:if test=".//note">
        <item id="h_{translate($fid, ' ', '_')}-not"
              href="{$fid}-not{$html_ext}" media-type="application/xhtml+xml"/>
        <xsl:apply-templates select=".//note" mode="manifest"/>
      </xsl:if>
      <!-- Images -->
      <xsl:choose>
        <xsl:when test="$cover and not($cover='=')">
          <item id="i_{translate($fid, ' ', '_')}_cover" href="{$img_dir}{$cover}">
            <xsl:call-template name="media_type_attribute">
              <xsl:with-param name="media">image</xsl:with-param>
              <xsl:with-param name="ext" select="concat('.', substring-after($cover, '.'))"/>
            </xsl:call-template>
          </item>
        </xsl:when>
        <xsl:when test="head/cover and ($img or $cover='=')">
          <xsl:apply-templates select="head/cover/image" mode="manifest"/>
        </xsl:when>
      </xsl:choose>
      <xsl:if test="$img">
        <xsl:apply-templates select=".//image[name(..)!='cover']" mode="manifest"/>
      </xsl:if>
      <!-- Math -->
      <xsl:if test="$math">
        <xsl:apply-templates select=".//math[latex]" mode="manifest"/>
      </xsl:if>
      <!-- CSS -->
      <xsl:call-template name="css_manifest"/>
      <!-- Extra -->
      <xsl:call-template name="manifest_extra"/>
    </manifest>
  </xsl:template>

  <xsl:template name="manifest_extra"/>

  <!--
      =========================================================================
      division, topic, note mode manifest
      =========================================================================
  -->
  <xsl:template match="division" mode="manifest">
    <xsl:if test="$toc_division_depth&gt;count(ancestor::division)">
    <item id="h_{translate($fid, ' ', '_')}-div-{count(preceding::division|ancestor::division)+1}"
          href="{$fid}-div-{count(preceding::division|ancestor::division)+1}{$html_ext}"
          media-type="application/xhtml+xml"/>
    </xsl:if>
  </xsl:template>

  <xsl:template match="topic" mode="manifest">
    <item id="h_{translate($fid, ' ', '_')}-tpc-{count(preceding::topic)+1}"
          href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}"
          media-type="application/xhtml+xml"/>
  </xsl:template>

  <xsl:template match="note" mode="manifest">
    <item id="h_{translate($fid, ' ', '_')}-not-{count(preceding::note)+1}"
          href="{$fid}-not-{count(preceding::note)+1}{$html_ext}"
          media-type="application/xhtml+xml"/>
  </xsl:template>

  <!--
      =========================================================================
      image mode manifest
      =========================================================================
  -->
  <xsl:template match="image" mode="manifest">
    <xsl:if test="count(preceding::image[@id=current()/@id])=0">
      <item id="i_{translate(@id, ' ', '_')}">
        <xsl:attribute name="href">
          <xsl:value-of select="concat($img_dir, @id)"/>
          <xsl:call-template name="image_extension"/>
        </xsl:attribute>
        <xsl:call-template name="media_type_attribute">
          <xsl:with-param name="media">image</xsl:with-param>
          <xsl:with-param name="ext">
            <xsl:call-template name="image_extension"/>
          </xsl:with-param>
        </xsl:call-template>
      </item>
    </xsl:if>
  </xsl:template>

  <!--
      =========================================================================
      math mode manifest
      =========================================================================
  -->
  <xsl:template match="math" mode="manifest">
    <item id="i_tex{count(preceding::math)+1}">
      <xsl:attribute name="href">
        <xsl:value-of
            select="concat($math_dir, $fid, '-tex-', count(preceding::math)+1,
                    '.png')"/>
      </xsl:attribute>
      <xsl:attribute name="media-type">image/png</xsl:attribute>
    </item>
  </xsl:template>

  <!--
      =========================================================================
      Template css_manifest
      =========================================================================
  -->
  <xsl:template name="css_manifest">
    <xsl:choose>
      <xsl:when test="$minify">
        <item id="c_styles" href="{$css_dir}styles.css" media-type="text/css"/>
      </xsl:when>
      <xsl:otherwise>
        <item id="c_publidoc" href="{$css_dir}publidoc.css" media-type="text/css"/>
        <item id="c_custom" href="{$css_dir}custom.css" media-type="text/css"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>


  <!--
      *************************************************************************
                                        SPINE
      *************************************************************************
  -->
  <!--
      =========================================================================
      Template spine
      =========================================================================
  -->
  <xsl:template name="spine">
    <spine toc="ncx">
      <xsl:if test="head/cover or ($cover and not($cover='='))">
        <itemref idref="h_{translate($fid, ' ', '_')}-cover" linear="no"/>
      </xsl:if>
      <xsl:if test="$toc">
        <itemref idref="h_{translate($fid, ' ', '_')}-toc"/>
      </xsl:if>
      <xsl:apply-templates select="../topic|division|topic" mode="spine"/>
      <xsl:if test="$index and .//index">
        <itemref idref="h_{translate($fid, ' ', '_')}-index"/>
      </xsl:if>
      <xsl:if test=".//note">
        <itemref idref="h_{translate($fid, ' ', '_')}-not"/>  <!-- linear="no" -->
        <xsl:apply-templates select=".//note" mode="spine"/>
      </xsl:if>
    </spine>
  </xsl:template>

  <!--
      =========================================================================
      division, topic, note mode spine
      =========================================================================
  -->
  <xsl:template match="division" mode="spine">
    <xsl:if test="$toc_division_depth&gt;count(ancestor::division)">
      <itemref idref="h_{translate($fid, ' ', '_')}-div-{count(preceding::division|ancestor::division)+1}"/>
    </xsl:if>
    <xsl:apply-templates select="division|topic" mode="spine"/>
  </xsl:template>

  <xsl:template match="topic" mode="spine">
    <itemref idref="h_{translate($fid, ' ', '_')}-tpc-{count(preceding::topic)+1}"/>
  </xsl:template>

  <xsl:template match="note" mode="spine">
    <itemref idref="h_{translate($fid, ' ', '_')}-not-{count(preceding::note)+1}"/>  <!-- linear="no" -->
  </xsl:template>


  <!--
      *************************************************************************
                                         GUIDE
      *************************************************************************
  -->
  <!--
      =========================================================================
      Template guide
      =========================================================================
  -->
  <xsl:template name="guide">
    <guide>
      <xsl:if test="head/cover or ($cover and not($cover='='))">
        <reference type="cover" href="{$fid}-cover{$html_ext}" title="{$i18n_cover}"/>
      </xsl:if>
      <xsl:if test="$toc">
        <reference type="toc" href="{$fid}-toc{$html_ext}" title="{$i18n_toc}"/>
      </xsl:if>
      <xsl:apply-templates select=".//topic" mode="guide"/>
      <reference type="text" href="{$fid}-tpc-1{$html_ext}" title="{$i18n_text}"/>
      <xsl:if test="$index and .//index">
        <reference type="index" href="{$fid}-index{$html_ext}" title="{$i18n_index}"/>
      </xsl:if>
    </guide>
  </xsl:template>

  <!--
      =========================================================================
      topic mode guide
      =========================================================================
  -->
  <xsl:template match="topic" mode="guide">
    <xsl:choose>
      <xsl:when test="@type='title' and not(head/title)
                      and not(preceding::topic[@type='title'])">
        <reference type="{@type}" href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}"
                   title="{$i18n_title_page}"/>
      </xsl:when>
      <xsl:when test="@type='title' and not(preceding::topic[@type='title'])">
        <reference type="{@type}" href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}"
                   title="{head/title}"/>
      </xsl:when>

      <xsl:when test="@type='copyright' and not(head/title)">
        <reference type="{@type}" href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}"
                   title="{$i18n_copyright}"/>
      </xsl:when>
      <xsl:when test="@type='copyright'">
        <reference type="{@type}" href="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}"
                   title="{head/title}"/>
      </xsl:when>
    </xsl:choose>
  </xsl:template>

</xsl:stylesheet>
