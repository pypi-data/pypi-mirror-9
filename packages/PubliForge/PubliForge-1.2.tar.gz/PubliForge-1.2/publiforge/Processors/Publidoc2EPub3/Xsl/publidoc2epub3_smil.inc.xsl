<?xml version="1.0" encoding="utf-8"?>
<!-- $Id: publidoc2epub3_smil.inc.xsl 5545df3f5bd2 2015/02/03 22:44:36 Patrick $ -->
<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns="http://www.w3.org/ns/SMIL"
                xmlns:date="http://exslt.org/dates-and-times"
                extension-element-prefixes="date">

  <!--
      =========================================================================
      topic mode smil
      =========================================================================
  -->
  <xsl:template match="topic" mode="smil">
    <xsl:if test="$aud and .//smil">
      <xsl:document href="{$path}{$fid}-tpc-{count(preceding::topic)+1}.smil" method="xml"
                    encoding="utf-8" indent="yes">
        <xsl:comment> Generated by PubliForge, <xsl:value-of select="date:date-time()"/> </xsl:comment>
        <smil version="3.0"> 
          <body>
            <xsl:apply-templates select=".//smil" mode="smil"/>
          </body>
        </smil>
      </xsl:document>
    </xsl:if>
  </xsl:template>

  <!--
      =========================================================================
      smil mode smil
      =========================================================================
  -->
  <xsl:template match="smil" mode="smil">
    <xsl:variable name="num" select="count(preceding::smil)+1"/>
    <xsl:variable name="audio">
      <xsl:choose>
        <xsl:when test="@audio">
          <xsl:value-of select="@audio"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="ancestor::section/head/audio[@type='smil']/@id"/>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:variable>
    
    <par id="s{$num}">
      <text src="{$fid}-tpc-{count(preceding::topic)+1}{$html_ext}#s{$num}"/>
      <audio>
        <xsl:attribute name="src">
          <xsl:value-of select="concat($aud_dir, $audio)"/>
          <xsl:choose>
            <xsl:when test="$aud_ext1='.mp3' or $aud_ext2='.mp3'">.mp3</xsl:when>
            <xsl:otherwise><xsl:value-of select="$aud_ext1"/></xsl:otherwise>
          </xsl:choose>
        </xsl:attribute>
        <xsl:attribute name="clipBegin">
          <xsl:choose>
            <xsl:when test="@begin">
              <xsl:value-of select="@begin"/>
            </xsl:when>
            <xsl:otherwise>0</xsl:otherwise>
          </xsl:choose>
          <xsl:text>s</xsl:text>
        </xsl:attribute>
        <xsl:attribute name="clipEnd">
          <xsl:choose>
            <xsl:when test="@end">
              <xsl:value-of select="@end"/>
            </xsl:when>
            <xsl:when test="following::smil[@begin and (@audio=$audio
                            or ancestor::section/head/audio[@type='smil']/@id=$audio)]">
              <xsl:value-of
                  select="following::smil[@begin and (@audio=$audio
                          or ancestor::section/head/audio[@type='smil']/@id=$audio)][1]/@begin"/>
            </xsl:when>
            <xsl:when test="@begin">
              <xsl:value-of select="translate(@begin+10, ',', '.')"/>
            </xsl:when>
            <xsl:otherwise>10</xsl:otherwise>
          </xsl:choose>
          <xsl:text>s</xsl:text>
        </xsl:attribute>
      </audio>
    </par>
  </xsl:template>
  
</xsl:stylesheet>
